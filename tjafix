#!/usr/bin/awk -f
# Copyright (c) 2019, Dakotah Lambert

function end () {
	if (inBranch) {
		print "#BRANCHEND"
		inBranch = 0
	}
	if (! ended) {
		print "#END"
		ended = 1
	}
	poststart = 0
}

# separate lines by CRLF
BEGIN { ended = 1; ORS = "\015\012"; level = -1 }

# strip CR from CRLF sequences if OS doesn't want it
{ sub("\015$", "") }

# strip comments
{ sub("//.*$", "") }
{ sub(";.*$", "") }

# ignore blank lines (and those consisting on only whitespace) before #START
/^[[:space:]]*$/ {
	if (poststart) {
		print
	}
	next
}

# put a blank line immediately before #START
$1 ~ /^#START/ {
	end()
	ended = 0
	# Default is Oni: be explicit about that
	if (! seenCourse) {
		print "COURSE:Oni"
	}
	# Make sure that LEVEL appears after COURSE
	# so that multi-difficulty files work right
	if (level >= 0) {
		print "LEVEL:" level
		level = -1
	}
	print ""
	print
	poststart = 1
	seenCourse = 0
	next
}

# in the header, remove spaces after colons
! poststart {
	sub(": ?",":")
}

# in multi-difficulty files, LEVEL must appear after COURSE.
# We'll enforce that.
/LEVEL:/ && ! seenCourse {
	split($0,a,":")
	level=a[2]
	next
}

# set the difficulty to a word, not a number
/COURSE:/ {
	seenCourse=1
	split($0, a, ":")
	difficulty = "Oni"
	a[2] = tolower(a[2])
	gsub("[[:space:]]", "", a[2])
	if (a[2] == 0 || a[2] == "easy") {
		difficulty = "Easy"
	} else if (a[2] == 1 || a[2] == "normal") {
		difficulty = "Normal"
	} else if (a[2] == 2 || a[2] == "hard") {
		difficulty = "Hard"
## The Ura/Edit difficulty displays as Oni in the menu, but is separate.
	} else if (a[2] == 4 || a[2] == "ura" || a[2] == "edit") {
		difficulty = "Edit"
	}
	$0 = "COURSE:" difficulty
}

# If the line starts with a note-like character
# and doesn't contain a colon (indicating metadata),
# it's probably (a part of) a measure.
# We walk through the notes and replace with 0
# anything that comes between a note that needs an end
# and its corresponding end.
$1 ~ /^[[:digit:]ABF]/ && ! /:/ {
	n=split($0, a, "")
	o=""
	for (i = 1; i <= n; ++i) {
		if (a[i] !~ "^[[:digit:]]+$") {
			; // no change
		} else if ((await && a[i] == 8) || (await9 && a[i] == 9)) {
			await = 0
			await9 = 0
		} else if (await) {
			a[i] = "0"
		} else if (a[i] ~ "[567]") {
			await = 1
		} else if (a[i] == "9") {
			await = 1
			await9 = 1
		}
		o = o a[i]
	}
	$0 = o
}

$1 ~ /^#/ {
	n=match($1, "[^#A-Z]")
	if (n != 0 && substr($1, n, 1) !~ "[[:space:]]") {
		$1 = substr($1, 1, n-1) " " substr($1, n, length - n + 1)
	}
}

$1 ~ /^#BRANCHSTART/ {
	inBranch = 1
}

$1 ~ /^#BRANCHEND/ {
	inBranch = 0
}

# colons are only in metadata.
# updating metadata should not happen during a song,
# so if we see an update, it should be after #END
/:/ { end() }

# If you see an #END while in a branch, end that branch.
$1 ~ /^#END/ { end(); next }

{print}

# if you reach EOF without ending the song, end it now.
END { end() }
